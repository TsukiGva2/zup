ARG.Usage usage = NEW("Usage: %0% [flags] name")
ARG.Bool newproject = NEW("n", "new", FALSE, "create a new project")
ARG.Bool delete = NEW("D", "delete", FALSE, "delete a project's folder")

PROC dieOnErr(status stat, string msg, bool fatal = TRUE)
  IF stat.Equal(FAIL)
    IF fatal
      LOG.fatal(msg)
    ELSE
      LOG.error(msg)
    }
  }
}

PROC delDir(string name)
  FOR file IN IO.dirList(name)
    file = name .. "/" .. file
    IF IO.isDirectory(file)
      delDir(file) # recursively delete directories
      #dieOnErr(IO.rmdir(file), "couldn't delete dir", FALSE)
    ELSE
      dieOnErr(IO.delete(file), "couldn't delete file " .. file, FALSE)
    }
  }
  dieOnErr(IO.rmdir(name), "error deleting dir")
}

FUNC Main() int
  list<string> args = ARG.getRawList()
  IF args.size() <= 1
    IO.print(usage.get())
    RETURN 1
  }

  string projectname = args[-1]
  
  IF newproject.get()
    
    dieOnErr(IO.mkdir(projectname), "error creating project dir")
    dieOnErr(IO.chdir(projectname), "error entering project dir")

    IO.File gitignore = IO.fileWriter(".gitignore", TRUE) # truncate = true
    dieOnErr(gitignore.writeAllLines(["ZUDIR/", "bin/", "*.exe", ""]), "error writing gitignore", FALSE)
    dieOnErr(gitignore.flush(), "error flushing .gitignore", FALSE)
    gitignore.close()

    dieOnErr(IO.mkdir("src"), "error creating source dir")
    dieOnErr(IO.chdir("src"), "error entering source dir")

    IO.File mainfile = IO.fileWriter("main.zu", TRUE)
    dieOnErr(mainfile.writeAllLines(["FUNC main() int", "\tRETURN 0", "}", ""]), "error writing main.zu", FALSE)
    dieOnErr(mainfile.flush(), "error flushing src/main.zu")
    mainfile.close()
    
    dieOnErr(IO.mkdir("bin"), "error creating bin dir")

  ELSEIF delete.get()
    
    IO.print("are you sure you want to delete " .. projectname .. "?\n")
    IF IO.readChar().asString().toLower() == "y"
      IF IO.isDirectory(projectname)
        delDir(projectname)
        IO.print("done")
      ELSE
        LOG.fatal(projectname .. " is not a directory")
      }
    ELSE
      IO.print("ok! be careful next time\n")
      RETURN 0
    }

  }

  RETURN 0
}
